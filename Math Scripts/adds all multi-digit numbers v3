Adds all inputted multidigit numbers in the form [n+n+...]
This one is longer than my other one but has a dynamic total that resizes automatically
	Note: spotted a bug; adding at least 12 (x) digits numbers without adding an [x+1] size number will break it
	ie adding 9 12 times
Not wrapping not negative cells

>(10++++++++++)

>>>>,
[
	[
		>(7+++++++)[<(6------)>-]<-
		[(4----)>>] < [>>>]<[>,>]<
	]
	<
	[
		-
		[
			[<]
			<[<]<<
			[-[[>]>](10++++++++++)<]
			>[-]
			>[>]>[>]<-
		]
		<[<]<[<]<<[->>+<<]
		+<[>->>[>]>[>]]
		>
		[
			moves everything over one to the right and adds an extra 10 in
			->>[>]>[>]<
			[[->+<]<]<[[->+<]<]<
			(10++++++++++)>>>[>]>[>]>
		]
		<<
	]
	<[<]>[[-<<+>>]>]>,
]

<<<<
[>(11+++++++++++)<[->-<]<]
>>
[
	-[<<]>
]
>+
[
	<(6++++++)[->(8++++++++)<]
	>-.[-]>
]
