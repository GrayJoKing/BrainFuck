734 bytes
Prints itself without the comments

Marker for end of data section
-
Data section
>++++>>+>+>++++>+>+++>+>+>++++>++>++++>++>>+>+++++>+++>+++>++++++>++>++++>++>+++>++>++>++++>+++++>+>++++>+>+++>+>++++>++>>+>+++++>+++>++>++++>++>+++>++>++>+++>+++++>+++>>+>+>+>+++++>+++++>++>++++>+>+++>>+>++++>+++++>+>++++>++>+++>>+>>>>>>>>+>>>>>>>>+>>>+>>>+>>+>>+>+++>>>++++++>+>++++>+++++>+++>+>++++>++>++>++>++++>+>>++>+++++>+++>+>++++>+>>++>+++++>+++>+>++++>+++++>++>++>++>>+>++++++>+>+>+++>+++++>++>++++++>+>+++>++>>>++>+++++>+++++>++++++>++>++++>+>+>>>>>++>>>>++>+++++>+++>++>+++++>+>+>++++>++>>>>+>+++++>+++>>>>>>+>++++>>+>+++>>>>>+>+++>+>+++>

+[<+]

Generates the plus and right characters
<(5+++++)[-<+++>]
<<->[->+++>++++<<]

Print the initial minus character
>.-->++>
[ Loop over each piece of data
	Print a right character
	<.>-
	Print a plus for each plus that made up this cell
	[<<.<+>>>-]
	Move the right and plus character over one
	<[->+<]<[->+<]>>>
]

<[-]<.++
[<+<+<++<++<+++++++<+++++++<+[>]<-]

<+[<]>--

<<<+
[
	-
	[
		>>[>]>[-<+>]
		<[<]<-
	]
	>>[>]>.[[-<+>]>]
	<<[<]<<+
]

+<>[]-.
43 60 62 91 93 45 46


734 bytes
Prints itself without the comments

Marker for end of data section
-
Data section
>++++>>+>+>++++>+>+++>+>+>++++>++>++++>++>>+>+++++>+++>+++>++++++>++>++++>++>+++>++>++>++++>+++++>+>++++>+>+++>+>++++>++>>+>+++++>+++>++>++++>++>+++>++>++>+++>+++++>+++>>+>+>+>+++++>+++++>++>++++>+>+++>>+>++++>+++++>+>++++>++>+++>>+>>>>>>>>+>>>>>>>>+>>>+>>>+>>+>>+>+++>>>++++++>+>++++>+++++>+++>+>++++>++>++>++>++++>+>>++>+++++>+++>+>++++>+>>++>+++++>+++>+>++++>+++++>++>++>++>>+>++++++>+>+>+++>+++++>++>++++++>+>+++>++>>>++>+++++>+++++>++++++>++>++++>+>+>>>>>++>>>>++>+++++>+++>++>+++++>+>+>++++>++>>>>+>+++++>+++>>>>>>+>++++>>+>+++>>>>>+>+++>+>+++>

+[<+]

Generates the plus and right characters
<(5+++++)[-<+++>]
<<->[->+++>++++<<]

Print the initial minus character
>.-->++>
[ Loop over each piece of data
	Print a right character
	<.>-
	Print a plus for each plus that made up this cell
	[<<.<+>>>-]
	Move the right and plus character over one
	<[->+<]<[->+<]>>>
]

<[-]<.++
[<+<+<++<++<+++++++<+++++++<+[>]<-]

<+[<]>--

<<<+
[
	-
	[
		>>[>]>[-<+>]
		<[<]<-
	]
	>>[>]>.[[-<+>]>]
	<<[<]<<+
]

+<>[]-.
43 60 62 91 93 45 46
